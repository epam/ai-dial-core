plugins {
    id "java"
    id 'checkstyle'
    id 'application'
    id 'io.freefair.lombok' version '8.0.1'
}

group = 'com.epam.aidial'
version = "0.16.1"

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(17)
    }
}

checkstyle {
    configDirectory = file("$rootProject.projectDir/checkstyle")
}

repositories {
    mavenCentral()
}

configurations.all {
    resolutionStrategy {
        force 'com.google.code.gson:gson:2.8.9'
    }
}

dependencies {
    implementation 'com.google.protobuf:protobuf-java:3.25.5'
    implementation 'org.slf4j:slf4j-api:1.7.32'
    implementation 'com.epam.deltix:gflog-api:3.0.6'
    implementation 'com.epam.deltix:gflog-core:3.0.6'
    runtimeOnly 'com.epam.deltix:gflog-slf4j:3.0.6'
    implementation 'io.vertx:vertx-core:4.5.8'
    implementation 'io.vertx:vertx-config:4.5.8'
    implementation 'io.vertx:vertx-opentelemetry:4.5.8'
    implementation 'io.vertx:vertx-micrometer-metrics:4.5.8'
    implementation 'io.micrometer:micrometer-registry-prometheus:1.11.2'
    implementation 'io.micrometer:micrometer-registry-otlp:1.11.2'
    implementation 'io.opentelemetry:opentelemetry-sdk-extension-autoconfigure:1.28.0'
    implementation 'io.opentelemetry:opentelemetry-exporter-otlp:1.28.0'
    implementation 'org.apache.commons:commons-lang3:3.12.0'
    implementation 'com.fasterxml.jackson.core:jackson-databind:2.15.2'
    implementation 'com.auth0:java-jwt:4.4.0'
    implementation 'com.auth0:jwks-rsa:0.22.1'
    // manually built jclouds fat jar
    // base commit https://github.com/apache/jclouds/commit/5fad7fa895c02c1a8394175032a47a8bb175e84d
    // cherry-pick commit https://github.com/apache/jclouds/commit/b379c17156237de96cedbb19d82bc86168fe83af
    // this version includes important google-cloud-storage fix that is not released yet
    implementation files("libs/jclouds-allblobstore-2.5.0-SNAPSHOT-jar-with-dependencies.jar")
    implementation ('org.apache.jclouds.api:filesystem:2.5.0') {
        exclude group: 'org.apache.jclouds', module: 'jclouds-blobstore'
        exclude group: 'org.apache.jclouds', module: 'jclouds-core'
    }
    implementation 'org.redisson:redisson:3.27.0'
    implementation group: 'com.amazonaws', name: 'aws-java-sdk-core', version: '1.12.663'
    implementation group: 'com.amazonaws', name: 'aws-java-sdk-sts', version: '1.12.663'
    implementation group: 'com.google.auth', name: 'google-auth-library-oauth2-http', version: '1.23.0'
    implementation group: 'com.azure', name: 'azure-identity', version: '1.13.2'

    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.9.3'
    testImplementation 'commons-io:commons-io:2.11.0'
    testImplementation 'io.vertx:vertx-web-client:4.5.8'
    testImplementation 'io.vertx:vertx-junit5:4.5.8'
    testImplementation 'org.mockito:mockito-core:5.7.0'
    testImplementation 'org.mockito:mockito-junit-jupiter:5.7.0'
    testImplementation 'org.apache.httpcomponents:httpclient:4.5.14'
    testImplementation('com.github.codemonstur:embedded-redis:1.4.3') {
        exclude group: 'org.slf4j', module: 'slf4j-simple'
    }
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.9.3'
}

application {
    mainClass = 'com.epam.aidial.core.AiDial'
}

distributions {
    // write current version to the version file
    beforeEvaluate {
        def versionFile = file("$projectDir/src/main/resources/version")
        versionFile.getParentFile().mkdirs()
        versionFile.createNewFile()
        versionFile.write(version)
    }
}

test {
    testLogging {
        events "passed", "skipped", "failed", "standardOut", "standardError"
        exceptionFormat = "full"
    }
    useJUnitPlatform()
}
